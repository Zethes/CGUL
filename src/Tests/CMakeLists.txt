# Jatta - General Utility Library
# Copyright (C) 2012-2013, Joshua Brookover and Amber Thrall
# All rights reserved.

# Tests for C++ features

INCLUDE(CheckIncludeFileCXX)

FUNCTION(CHECK_HEADER HEADER)
  STRING(TOUPPER ${HEADER} HEADER_UPPER)
  CHECK_INCLUDE_FILE_CXX(${HEADER} ${HEADER_UPPER}_FOUND)
  IF (${HEADER_UPPER}_FOUND)
    SET(CPP_HEADER_${HEADER_UPPER} ON CACHE BOOL "Does C++ have <${HEADER}>?")
    SET(JATTA_CPP_CONFIG "${JATTA_CPP_CONFIG}\n#define _CPP_HEADER_${HEADER_UPPER}" PARENT_SCOPE)
  ELSE()
    SET(CPP_HEADER_${HEADER_UPPER} OFF CACHE BOOL "Does C++ have <${HEADER}>?")
  ENDIF()
  MARK_AS_ADVANCED(CPP_HEADER_${HEADER_UPPER})
ENDFUNCTION()

FUNCTION(CHECK_TEST TEST DESCRIPTION CACHE_NAME)
  TRY_COMPILE(${CACHE_NAME}_COMPILES ${CMAKE_CURRENT_BINARY_DIR}
${CMAKE_CURRENT_SOURCE_DIR}/${TEST})
  IF(${CACHE_NAME}_COMPILES)
    SET(${CACHE_NAME} ON CACHE BOOL ${DESCRIPTION})
    SET(JATTA_CPP_CONFIG "${JATTA_CPP_CONFIG}\n#define _${CACHE_NAME}" PARENT_SCOPE)
  ELSE()
    SET(${CACHE_NAME} OFF CACHE BOOL ${DESCRIPTION})
  ENDIF()
  MARK_AS_ADVANCED(${CACHE_NAME})
ENDFUNCTION()

SET(JATTA_CPP_CONFIG "// == SUPPORTED C++ HEADERS == //")

CHECK_HEADER(algorithm)
CHECK_HEADER(array)
CHECK_HEADER(bitset)
CHECK_HEADER(chrono)
CHECK_HEADER(condition_variable)
CHECK_HEADER(deque)
CHECK_HEADER(exception)
CHECK_HEADER(forward_list)
CHECK_HEADER(fstream)
CHECK_HEADER(functional)
CHECK_HEADER(future)
CHECK_HEADER(iomanip)
CHECK_HEADER(ios)
CHECK_HEADER(iosfwd)
CHECK_HEADER(iostream)
CHECK_HEADER(istream)
CHECK_HEADER(iterator)
CHECK_HEADER(limits)
CHECK_HEADER(list)
CHECK_HEADER(locale)
CHECK_HEADER(map)
CHECK_HEADER(memory)
CHECK_HEADER(mutex)
CHECK_HEADER(new)
CHECK_HEADER(ostream)
CHECK_HEADER(queue)
CHECK_HEADER(regex)
CHECK_HEADER(set)
CHECK_HEADER(sstream)
CHECK_HEADER(stack)
CHECK_HEADER(stdexcept)
CHECK_HEADER(streambuf)
CHECK_HEADER(string)
CHECK_HEADER(thread)
CHECK_HEADER(tuple)
CHECK_HEADER(typeinfo)
CHECK_HEADER(unordered_map)
CHECK_HEADER(unordered_set)
CHECK_HEADER(utility)
CHECK_HEADER(vector)

SET(JATTA_CPP_CONFIG "${JATTA_CPP_CONFIG}\n\n// == SUPPORTED C++ FEATURES == //")

CHECK_TEST("enum_class.cpp" "Does C++ have enum class?" CPP_ENUM_CLASS)
CHECK_TEST("nullptr.cpp" "Does C++ have nullptr?" CPP_NULLPTR)
CHECK_TEST("static_assert.cpp" "Does C++ have static_assert?" CPP_STATIC_ASSERT)
CHECK_TEST("std_move.cpp" "Does C++ have std::move?" CPP_STD_MOVE)
CHECK_TEST("std_thread.cpp" "Does C++ have std::thread?" CPP_STD_THREAD)

SET(JATTA_CPP_CONFIG ${JATTA_CPP_CONFIG} PARENT_SCOPE)